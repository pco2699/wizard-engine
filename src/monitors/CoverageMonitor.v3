// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that tracks code coverage.
class CoverageMonitor extends Monitor {
	def entries = Vector<CoverageMonitorProbe>.new();
	def executed = Vector<bool>.new();

	def onParse(m: Module) {
		for (i < m.functions.length) {
			var f = m.functions[i];
			if (f.imp != null) continue; // skip imported functions
			var p = CoverageMonitorProbe.new(m, f, this, executed.length);
			entries.put(p);
			executed.putn(false, f.cur_bytecode.length);
			for (bi = BytecodeIterator.new().reset(f); bi.more(); bi.next()) {
				bi.current();
				m.insertProbeAt(f.func_index, bi.pc, p);
			}
		}
	}
	def onExit(code: int) {
		onFinish(null);
	}
	def onFinish(i: Instance) {
		var prev: FuncDecl;
		var tracer = InstrTracer.new(), d = DataReader.new([]);
		for (j < entries.length) {
			var e = entries[j];
			Trace.OUT.put1("func %q:", e.func.render(e.module.names, _)).outln();
			for (bi = BytecodeIterator.new().reset(e.func); bi.more(); bi.next()) {
				d.reset(e.func.orig_bytecode, bi.pc, e.func.orig_bytecode.length);
				if (executed[e.start + bi.pc]) {
					Trace.OUT.puts("   * ");
				} else {
					Trace.OUT.puts("     ");
				}
				tracer.putInstr(Trace.OUT, e.module, d);
				Trace.OUT.outln();
			}
		}
	}
}
class CoverageMonitorProbe(module: Module, func: FuncDecl, monitor: CoverageMonitor, start: int) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> Resumption {
		monitor.executed[dynamicLoc.pc + start] = true;
		module.removeProbeAt(dynamicLoc.func.decl.func_index, dynamicLoc.pc, this);
		return Resumption.Continue;
	}
}